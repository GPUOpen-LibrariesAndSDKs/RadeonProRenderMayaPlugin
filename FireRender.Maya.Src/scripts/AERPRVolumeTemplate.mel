//
// Copyright 2020 Advanced Micro Devices, Inc
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//    http://www.apache.org/licenses/LICENSE-2.0
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

global proc AEVolumeCheckEnableAlbedo(string $nodeName)
{
	string $nodeAttr = $nodeName + ".albedoEnabled";
	int $enabled = `getAttr $nodeAttr`;

	editorTemplate -dimControl $nodeName "albedoSelectedGrid" ($enabled == 0);
	editorTemplate -dimControl $nodeName "volumeDimensionsAlbedo" true;
}

global proc AEVolumeCheckEnableEmission(string $nodeName)
{
	string $nodeAttr = $nodeName + ".emissionEnabled";
	int $enabled = `getAttr $nodeAttr`;
	
	editorTemplate -dimControl $nodeName "emissionSelectedGrid" ($enabled == 0);
	editorTemplate -dimControl $nodeName "volumeDimensionsEmission" true;
	editorTemplate -dimControl $nodeName "emissionIntensity" ($enabled == 0);
	editorTemplate -dimControl $nodeName "emissionKelvin" ($enabled == 0);
	editorTemplate -dimControl $nodeName "emissionInputType" ($enabled == 0);

	AERPRVolumeInputType($nodeName);
}

global proc AEVolumeCheckEnableDensity(string $nodeName)
{
	string $nodeAttr = $nodeName + ".densityEnabled";
	int $enabled = `getAttr $nodeAttr`;
	
	editorTemplate -dimControl $nodeName "densitySelectedGrid" ($enabled == 0);
	editorTemplate -dimControl $nodeName "volumeDimensionsDensity" true;
	editorTemplate -dimControl $nodeName "volumeVoxelSizeDensity" true;
	editorTemplate -dimControl $nodeName "densityMultiplier" ($enabled == 0);
}

global proc importVDBFilePathNew( string $attr )
{
	global int $gAttributeEditorTemplateLabelWidth;
    rowLayout
        -numberOfColumns 3
        -columnWidth3 $gAttributeEditorTemplateLabelWidth 175 35
        -columnAttach3 "right" "both" "both"
        -columnAlign3 "left" "left" "center"
        -rowAttach 1 "both" 0
        -rowAttach 2 "both" 0
        -rowAttach 3 "both" 0
        importVDBFilePathLayout;
    {
        text
            -label "VDB File Path"
            -annotation ""
            importVDBFilePathLabel;

        textField
            -fileName "vdb file path and name. Insert a sequence of pound signs (#) and connect the global frame time node to embed the current frame number in the file name. (The number of pound signs defines the zero padding width.)"
            -insertionPosition 0
            -editable true
            -annotation "vdb file path and name. Insert a sequence of pound signs (#) and connect the global frame time node to embed the current frame number in the file name. (The number of pound signs defines the zero padding width.)"
            importVDBFilePathField;

        button
            -label "..."
            -align "center"
            -command ""
            -annotation "Browse for the VDB file."
            importVDBFilePathButton;

        setParent ..;
    }

    importVDBFilePathReplace( $attr );
}

global proc importVDBFilePathReplace( string $attr )
{
    setParent importVDBFilePathLayout;
    connectControl importVDBFilePathField $attr;
    button -e
        -command ("browseForVDBFile( \""+$attr+"\" )")
        importVDBFilePathButton;
    setParent ..;
}

global proc browseForVDBFile( string $filePathAttr )
{
    string $currentFilePath = `getAttr $filePathAttr`;
    string $currentFilePathDir = dirname( $currentFilePath );

    if( $currentFilePath != "" )
    {
        if( filetest( "-d", $currentFilePath ) == 1 )
        {
            $currentFilePathDir = $currentFilePath;
        }
    }

    string $singleFilter = "VDB Files (*.vdb)";

    string $path[] = `fileDialog2 -ds 2 -cap "VDB File" -dir $currentFilePathDir -ff $singleFilter -fm 1`;

    if (size($path) > 0)
    {
        setAttr -type "string" $filePathAttr $path[0];
    }
}

global proc FillGridList( string $nodeName )
{
	string $listAttrName = $nodeName + ".loadedGrids";

	string $attrNameA = $nodeName + ".albedoSelectedGrid";
	albedoGridSelectReplace($attrNameA, $listAttrName);

	string $attrNameD = $nodeName + ".densitySelectedGrid";
	densityGridSelectReplace($attrNameD, $listAttrName);

	string $attrNameE = $nodeName + ".emissionSelectedGrid";
	emissionGridSelectReplace($attrNameE, $listAttrName);

	updateAE $nodeName;
}

global proc albedoGridSelectNew( string $attrName, string $listAttrName )
{
	paneLayout;
			textScrollList 
				-allowMultiSelection false 
				-numberOfRows 4 
				-selectCommand onAlbedoGridSelectClicked
				albedoGridList;
	setParent ..;

	connectControl albedoGridList $attrName;

	albedoGridSelectReplace($attrName, $listAttrName);
}

global proc albedoGridSelectReplace( string $attrName, string $listAttrName )
{	
	vdbGridSelectReplace($attrName, $listAttrName, "albedoGridList");
	$isselectd = `textScrollList -q -selectItem albedoGridList`;
	if (size($isselectd) == 0)
	{
		textScrollList -e -selectIndexedItem 1 albedoGridList;
		onAlbedoGridSelectClicked();
	}
}

global proc vdbGridSelectReplace(string $attrName, string $listAttrName, string $widgetName)
{
	connectControl $widgetName $attrName;

	textScrollList -e -removeAll $widgetName;
	
	string $multiAttr[] = `listAttr -multi ( $listAttrName )`;
	for ( $attr in $multiAttr )
	{
		string $buffer[];
		tokenize $attr "[" $buffer;

		$len = size($listAttrName) - size($attr) + size($buffer[1]) + 1;
		string $tmpstr = $listAttrName;
		string $nodestr = `substring $tmpstr 1 $len`;
		$attr = $nodestr + $attr;
		
		$val = `getAttr $attr`;
		textScrollList -e -append $val $widgetName;
	}

	string $selectedValue = `getAttr $attrName`;

	string $listAllItems[];
	$listAllItems = `textScrollList -q -allItems $widgetName`;
	
	for ($tmp in $listAllItems)
	{
		if ($tmp == $selectedValue)
		{
			textScrollList -e -selectItem $selectedValue $widgetName;
			break;
		}
	}
}

global proc onAlbedoGridSelectClicked()
{
	string $selectedItems[];
	$selectedItems = `textScrollList -q -selectItem albedoGridList`;
	
	$attrName = `attrControlGrp -query -attribute albedoGridList`;
	setAttr $attrName -type "string" $selectedItems[0];
}

global proc densityGridSelectNew( string $attrName, string $listAttrName )
{
	paneLayout;
			textScrollList 
				-allowMultiSelection false 
				-numberOfRows 4 
				-selectCommand onDensityGridSelectClicked
				densityGridList;
	setParent ..;

	connectControl densityGridList $attrName;

	densityGridSelectReplace($attrName, $listAttrName);
}

global proc densityGridSelectReplace( string $attrName, string $listAttrName )
{	
	vdbGridSelectReplace($attrName, $listAttrName, "densityGridList");

	// without selected density grid volume will not render (and will not give any warnings!)
	// thus this safety routine is added
	$isselectd = `textScrollList -q -selectItem densityGridList`;
	if (size($isselectd) == 0)
	{
		textScrollList -e -selectIndexedItem 1 densityGridList;
		onDensityGridSelectClicked();
	}
}

global proc onDensityGridSelectClicked()
{
	string $selectedItems[];
	$selectedItems = `textScrollList -q -selectItem densityGridList`;
	
	$attrName = `attrControlGrp -query -attribute densityGridList`;
	setAttr $attrName -type "string" $selectedItems[0];
}

global proc emissionGridSelectNew( string $attrName, string $listAttrName )
{
	paneLayout;
			textScrollList 
				-allowMultiSelection false 
				-numberOfRows 4 
				-selectCommand onEmissionGridSelectClicked
				emissionGridList;
	setParent ..;

	connectControl emissionGridList $attrName;

	emissionGridSelectReplace($attrName, $listAttrName);
}

global proc emissionGridSelectReplace( string $attrName, string $listAttrName )
{	
	vdbGridSelectReplace($attrName, $listAttrName, "emissionGridList");
	$isselectd = `textScrollList -q -selectItem emissionGridList`;
	if (size($isselectd) == 0)
	{
		textScrollList -e -selectIndexedItem 1 emissionGridList;
		onEmissionGridSelectClicked();
	}
}

global proc onEmissionGridSelectClicked()
{
	string $selectedItems[];
	$selectedItems = `textScrollList -q -selectItem emissionGridList`;
	
	$attrName = `attrControlGrp -query -attribute emissionGridList`;
	setAttr $attrName -type "string" $selectedItems[0];
}

global proc uiVDBFileSchemaNew( string $attr )
{
	optionMenu 
		-label "Frame Extention" 
		-enable true 
		vdbSequenceNamingSchema;
	menuItem 
		-label (uiRes("m_createMayaSoftwareCommonGlobalsTab.kExt3")) 
		-data 0
		mayaSoftwareNameDotFrameDotExtension;
	menuItem 
		-label (uiRes("m_createMayaSoftwareCommonGlobalsTab.kExt4")) 
		-data 1
		mayaSoftwareNameDotExtensionDotFrame;
	menuItem 
		-label (uiRes("m_createMayaSoftwareCommonGlobalsTab.kExt5")) 
		-data 2
		mayaSoftwareNameDotFrame;
	menuItem 
		-label (uiRes("m_createMayaSoftwareCommonGlobalsTab.kExt6")) 
		-data 3
		mayaSoftwareFrameDotExtension;
	menuItem 
		-label (uiRes("m_createMayaSoftwareCommonGlobalsTab.kExt7")) 
		-data 4
		mayaSoftwareNameUnderFrameDotExtension;
		
    connectControl vdbSequenceNamingSchema $attr;

    setParent ..;
}

global proc uiVDBFileSchemaReplace( string $attr )
{
    connectControl vdbSequenceNamingSchema $attr;
    
    setParent ..;
}

global proc AERPRVolumeInputType(string $nodeName)
{
	string $nodeAttr = $nodeName + ".emissionEnabled";
	int $enabled = `getAttr $nodeAttr`;

	if ($enabled == 1) {
		int $disable[];
		int $type = `getAttr ($nodeName + ".emissionInputType")`;

		if ($type == 0) {			// intensity+ramp
			$disable = {0, 1};
		} else if ($type == 1) {	// kelvin
			$disable = {1, 0};
		}

		editorTemplate -dimControl $nodeName "emissionIntensity"	$disable[0];
		editorTemplate -dimControl $nodeName "emissionRamp"			$disable[0];
		editorTemplate -dimControl $nodeName "emissionKelvin"		$disable[1];
	}
}

source "common.mel";
global proc AERPRVolumeTemplate(string $nodeName)
{
	editorTemplate -beginScrollLayout;
		editorTemplate -beginLayout "General" -collapse 0;
			editorTemplate -beginLayout "OpenVDB Read File" -collapse 0;
			{
				editorTemplate -callCustom "importVDBFilePathNew" "importVDBFilePathReplace" "VdbFilePath";
			}
			editorTemplate -endLayout;
			editorTemplate -beginLayout "Sequence Naming Schema" -collapse 0;
			{
				editorTemplate -callCustom "uiVDBFileSchemaNew" "uiVDBFileSchemaReplace" "namingSchema";
			}

		editorTemplate -endLayout;

	// we have to add "|" to the beginning so we can remove node name from this string later. Yes, it is dumb.
	string $colorAlbedoString   = "|0.5, 0.5, 0.5, 0.5, 0.5, 0.5";
	string $posAlbedoString	    = "|0.0, 1.0";
	string $albedoRampName	    = "|albedoRamp";

	string $colorEmissionString = "|0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.2, 0.0, 0.0, 0.6, 0.2, 0.0, 0.6, 1.0, 0.0, 1.0, 1.0, 1.0";
	string $posEmissionString	= "|0.0, 0.2, 0.4, 0.6, 0.8, 1.0";
	string $emissionRampName	= "|emissionRamp";

	string $colorDensityString  = "|0.5, 0.7, 0.8, 1.0";
	string $posDensityString    = "|0.0, 0.333, 0.667, 1.0";
	string $densityRampName		= "|densityRamp";

		editorTemplate -beginLayout "Albedo" -collapse 0 AlbedoLayout;
			editorTemplate -label "Enable" -addControl "albedoEnabled" "AEVolumeCheckEnableAlbedo";
			editorTemplate -label "Grid Dimensions" -addControl "volumeDimensionsAlbedo";
			editorTemplate -callCustom "albedoGridSelectNew" "albedoGridSelectReplace" "albedoSelectedGrid" "loadedGrids";
			editorTemplate -callCustom "AERPRCreateDefaultRampColors" "AERPRCreateDefaultRampColors" $nodeName $colorAlbedoString $posAlbedoString $albedoRampName;
			AEaddRampControl ($nodeName + ".albedoRamp");
		editorTemplate -endLayout;

		editorTemplate -beginLayout "Emission" -collapse 0 EmissionLayout;
			editorTemplate -label "Enable" -addControl "emissionEnabled" "AEVolumeCheckEnableEmission";
			editorTemplate -addControl "emissionInputType" AERPRVolumeInputType;
			editorTemplate -label "Grid Dimensions" -addControl "volumeDimensionsEmission";
			editorTemplate -callCustom "emissionGridSelectNew" "emissionGridSelectReplace" "emissionSelectedGrid" "loadedGrids";
			editorTemplate -callCustom "AERPRCreateDefaultRampColors" "AERPRCreateDefaultRampColors" $nodeName $colorEmissionString $posEmissionString $emissionRampName;
			AEaddRampControl ($nodeName + ".emissionRamp");
			editorTemplate -label "Intensity Multiplier" -addControl "emissionIntensity";
			editorTemplate -label "Kelvins" -addControl "emissionKelvin";
		editorTemplate -endLayout;

		editorTemplate -beginLayout "Density" -collapse 0 DensityLayout;
			editorTemplate -label "Enable" -addControl "densityEnabled" "AEVolumeCheckEnableDensity";
			editorTemplate -label "Grid Dimensions" -addControl "volumeDimensionsDensity";
			editorTemplate -label "Voxel size" -addControl "volumeVoxelSizeDensity";
			editorTemplate -callCustom "densityGridSelectNew" "densityGridSelectReplace" "densitySelectedGrid" "loadedGrids";
			editorTemplate -callCustom "AERPRCreateDefaultRampFloats" "AERPRCreateDefaultRampFloats" $nodeName $colorDensityString $posDensityString $densityRampName;
			editorTemplate -label "Density Grid" -addControl "densityGradType";
			AEaddRampControl ($nodeName + ".densityRamp");
			editorTemplate -label "Multiplier" -addControl "densityMultiplier";
		editorTemplate -endLayout;

	editorTemplate -addExtraControls;
	editorTemplate -endScrollLayout;
}
