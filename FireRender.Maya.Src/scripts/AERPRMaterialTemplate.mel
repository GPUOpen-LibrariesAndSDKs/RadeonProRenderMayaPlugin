//
// Copyright 2020 Advanced Micro Devices, Inc
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//    http://www.apache.org/licenses/LICENSE-2.0
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

global proc AERPRMaterialType(string $nodeName)
{
	int $disable[];
	int $type = `getAttr ($nodeName + ".type")`;
	if ($type == 0) {			// diffuse
		$disable = {0, 1, 1, 1, 0};
	} else if ($type == 1) {	// microfacet
		$disable = {0, 0, 0, 1, 0};
	} else if ($type == 2) {	// microfacet refract
		$disable = {0, 0, 0, 1, 0};
	} else if ($type == 3) {	// reflect
		$disable = {0, 1, 1, 1, 0};
	} else if ($type == 4) {	// refract
		$disable = {0, 1, 0, 1, 0};
	} else if ($type == 5) {	// transparent
		$disable = {0, 1, 1, 1, 1};
	} else if ($type == 6) {    // emissive
		$disable = {0, 1, 1, 0, 1};
	} else if ($type == 7) {    // ward
		$disable = {0, 1, 0, 1, 0};
	} else if ($type == 8) {    // oren
		$disable = {0, 0, 1, 1, 0};
	} else if ($type == 9) {    // diffuse refraction
		$disable = {0, 0, 1, 1, 0};
	} else if ($type == 10) {    // passthrough
		$disable = {0, 1, 1, 1, 1};
	}

	editorTemplate -dimControl $nodeName "color"            $disable[0];
	editorTemplate -dimControl $nodeName "roughness"        $disable[1];
	editorTemplate -dimControl $nodeName "refractiveIndex"  $disable[2];
	editorTemplate -dimControl $nodeName "wattsPerSqm"      $disable[3];
	editorTemplate -dimControl $nodeName "normalMap"        $disable[4];
}

global proc AERPRMaterialTemplate( string $nodeName )
{
	AEswatchDisplay $nodeName;
	editorTemplate -beginScrollLayout;

	editorTemplate -beginLayout "Swatch properties" -collapse true;
		editorTemplate -l "Disable" -addControl "disableSwatch";
		editorTemplate -l "Iterations" -addControl "swatchIterations";
	editorTemplate -endLayout;

	editorTemplate -addControl "type" AERPRMaterialType;
	editorTemplate -beginLayout "Common Material Attributes" -collapse 0;
		editorTemplate -addControl "color";
		editorTemplate -addControl "roughness";
		editorTemplate -addControl "refractiveIndex";
	editorTemplate -endLayout;
	editorTemplate -beginLayout "Emission" -collapse 0;
		editorTemplate -addControl "wattsPerSqm";
	editorTemplate -endLayout;
	editorTemplate -beginLayout "Normal Map" -collapse 0;
		editorTemplate -addControl "normalMap";
	editorTemplate -endLayout;
	AEdependNodeTemplate $nodeName;

	// editorTemplate -suppress "transparency";

	editorTemplate -addExtraControls;
	editorTemplate -endScrollLayout;
}